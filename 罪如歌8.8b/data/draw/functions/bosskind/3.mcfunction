#清理怪物
tp @e[tag=monster,tag=draw] ~ ~-1000 ~
kill @e[tag=monster,tag=draw]
##boss生成點 655 12 -1920
summon skeleton 655 12 -1920 {Team:"monster",Tags:["panling","undead","monster","draw_boss","draw","dlc","0","fk80"],CanPickUpLoot:false,ArmorDropChances:[0f,0f,0f,0f],HandDropChances:[0f,0f],CustomNameVisible:1b,CustomName:'{"text":"揹負罪孽之人","color": "gold","italic": false}',DeathLootTable:"ice:diaoluowu",Health:1000.0f,Attributes:[ {Name:"generic.max_health",Base:1000d},{Name:"generic.follow_range",Base:30d},{Name:"generic.movement_speed",Base:0.2d},{Name:"generic.attack_damage",Base:20d},{Name:"generic.knockback_resistance",Base:1000d},{Name:"generic.armor",Base:30d},{Name:"generic.armor_toughness",Base:20d}],ArmorItems:[{id:"minecraft:diamond_boots",Count:1b,tag:{Unbreakable:1b,Enchantments:[{id:"minecraft:protection",lvl:5s}]}},{id:"minecraft:diamond_leggings",Count:1b,tag:{Unbreakable:1b,Enchantments:[{id:"minecraft:protection",lvl:5s}]}},{id:"minecraft:diamond_chestplate",Count:1b,tag:{Unbreakable:1b,Enchantments:[{id:"minecraft:protection",lvl:5s}]}},{id:"minecraft:diamond_helmet",Count:1b,tag:{Enchantments:[{id:"minecraft:protection",lvl:5s}]}}],HandItems:[{},{}]}
#可無敵
scoreboard players set #dlc draw.bossskill.protect 1
#隨機武器
kill @e[tag=draw_choose]
summon marker 655 12 -1920 {Tags:["draw_choose","1"]}
summon marker 655 12 -1920 {Tags:["draw_choose","2"]}
summon marker 655 12 -1920 {Tags:["draw_choose","3"]}
summon marker 655 12 -1920 {Tags:["draw_choose","4"]}
summon marker 655 12 -1920 {Tags:["draw_choose","5"]}
summon marker 655 12 -1920 {Tags:["draw_choose","6"]}
summon marker 655 12 -1920 {Tags:["draw_choose","7"]}
summon marker 655 12 -1920 {Tags:["draw_choose","8"]}

execute if score #dlc draw.bossskill.mainskill.3.tag matches 1 run kill @e[tag=draw_choose,tag=1]
execute if score #dlc draw.bossskill.mainskill.3.tag matches 2 run kill @e[tag=draw_choose,tag=2]
execute if score #dlc draw.bossskill.mainskill.3.tag matches 3 run kill @e[tag=draw_choose,tag=3]
execute if score #dlc draw.bossskill.mainskill.3.tag matches 4 run kill @e[tag=draw_choose,tag=4]
execute if score #dlc draw.bossskill.mainskill.3.tag matches 5 run kill @e[tag=draw_choose,tag=5]
execute if score #dlc draw.bossskill.mainskill.3.tag matches 6 run kill @e[tag=draw_choose,tag=6]
execute if score #dlc draw.bossskill.mainskill.3.tag matches 7 run kill @e[tag=draw_choose,tag=7]
execute if score #dlc draw.bossskill.mainskill.3.tag matches 8 run kill @e[tag=draw_choose,tag=8]

tag @e[tag=draw_choose,limit=1,sort=random] add choosed
execute if entity @e[tag=draw_choose,tag=1,tag=choosed] run scoreboard players set #dlc draw.bossskill.mainskill.3.tag 1
execute if entity @e[tag=draw_choose,tag=2,tag=choosed] run scoreboard players set #dlc draw.bossskill.mainskill.3.tag 2
execute if entity @e[tag=draw_choose,tag=3,tag=choosed] run scoreboard players set #dlc draw.bossskill.mainskill.3.tag 3
execute if entity @e[tag=draw_choose,tag=4,tag=choosed] run scoreboard players set #dlc draw.bossskill.mainskill.3.tag 4
execute if entity @e[tag=draw_choose,tag=5,tag=choosed] run scoreboard players set #dlc draw.bossskill.mainskill.3.tag 5
execute if entity @e[tag=draw_choose,tag=6,tag=choosed] run scoreboard players set #dlc draw.bossskill.mainskill.3.tag 6
execute if entity @e[tag=draw_choose,tag=7,tag=choosed] run scoreboard players set #dlc draw.bossskill.mainskill.3.tag 7
execute if entity @e[tag=draw_choose,tag=8,tag=choosed] run scoreboard players set #dlc draw.bossskill.mainskill.3.tag 8

kill @e[tag=draw_choose]
#觸發武器效果
execute if score #dlc draw.bossskill.mainskill.3.tag matches 1 run function draw:mainskill/3/01/start
execute if score #dlc draw.bossskill.mainskill.3.tag matches 2 run function draw:mainskill/3/02/start
execute if score #dlc draw.bossskill.mainskill.3.tag matches 3 run function draw:mainskill/3/03/start
execute if score #dlc draw.bossskill.mainskill.3.tag matches 4 run function draw:mainskill/3/04/start
execute if score #dlc draw.bossskill.mainskill.3.tag matches 5 run function draw:mainskill/3/11/start
execute if score #dlc draw.bossskill.mainskill.3.tag matches 6 run function draw:mainskill/3/12/start
execute if score #dlc draw.bossskill.mainskill.3.tag matches 7 run function draw:mainskill/3/13/start
execute if score #dlc draw.bossskill.mainskill.3.tag matches 8 run function draw:mainskill/3/14/start
#武器生成後會給周邊玩家設置不應有的分數，將其清除以免影響所有玩家使用鍛造臺
execute positioned 655 12 -1920 run scoreboard players set @p dzsuccess 0